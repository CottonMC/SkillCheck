plugins {
	id "fabric-loom" version "0.5-SNAPSHOT"
	id 'com.github.johnrengelman.shadow' version '6.1.0'
	id "maven-publish"
	id "com.jfrog.artifactory" version "4.9.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

if(rootProject.file("private.gradle").exists()) { //Publishing details
	apply from: "private.gradle"
}

archivesBaseName = "skillcheck"
group = "io.github.cottonmc"
version = "2.0.0+1.14.4"

minecraft {
	refmapName = "mixins.skillcheck.refmap.json"
}

repositories {
	maven {
		url "https://server.bbkr.space/artifactory/libs-release"
	}
	maven {
		url "https://dl.bintray.com/ladysnake/libs"
	}
	maven {
		url "https://minecraft.curseforge.com/api/maven"
	}
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi ("io.github.cottonmc.cotton:cotton-config:1.0.0-rc.7") {
		transitive = false
	}
	modRuntime ("io.github.cottonmc.cotton:cotton-logging:1.0.0-rc.4") {
		transitive = false
	}
	// cloth config
	modApi("me.shedaniel.cloth:config-2:4.8.2") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "me.shedaniel.cloth:config-2:4.8.2"

	// mod menu
	modImplementation("io.github.prospector:modmenu:1.14.6+build.31") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modApi "io.github.cottonmc:CottonRPG:0.3.0"
	include "io.github.cottonmc:CottonRPG:0.3.0"
	modImplementation ("io.github.cottonmc:Jankson-Fabric:2.0.1+j1.2.0") { transitive = false }
	include "io.github.cottonmc:Jankson-Fabric:2.0.1+j1.2.0"

	compileOnly ("com.google.code.findbugs:jsr305:3.0.2") { transitive = false }
}

processResources {
	inputs.property "version", project.version
	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand version: project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		maven(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			//artifact(jar) {
			//	builtBy remapJar
			//}
			artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar") { //release jar - file location not provided anywhere in loom
				classifier null
				builtBy remapJar
			}

			artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}-dev.jar") { //release jar - file location not provided anywhere in loom
				classifier "dev"
				builtBy remapJar
			}

			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

artifactory {
	if (project.hasProperty("artifactoryUsername")) {
		contextUrl = "http://server.bbkr.space:8081/artifactory/"
		publish {
			repository {
				if (version.contains("SNAPSHOT")) {
					repoKey = "libs-snapshot"
				} else {
					repoKey = "libs-release"
				}

				username = artifactoryUsername
				password = artifactoryPassword
			}
			defaults {
				publications("maven")

				publishArtifacts = true
				publishPom = true
			}
		}
	} else {
		println "Cannot configure artifactory; please define ext.artifactoryUsername and ext.artifactoryPassword before running artifactoryPublish"
	}
}
